buildscript {
    ext {
        springBootVersion = '2.1.1.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.1.1.RELEASE")
    }
}

plugins {
    id 'org.springframework.boot' version '2.1.1.RELEASE'
    id "com.gorylenko.gradle-git-properties" version "2.0.0-beta1"
}

// Apply the java plugin to add support for Java
apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

project.sourceCompatibility = 11
project.targetCompatibility = 11

def applicationName = 'custody-api'
def today = java.time.Instant.now()
def todayDate = java.time.LocalDate.now()
def formatter = java.time.format.DateTimeFormatter.ofPattern("yyyy-MM-dd")
version todayDate.format(formatter)

jar {
    baseName = applicationName
    manifest {
        attributes 'Implementation-Title': applicationName,
                'Implementation-Version': System.getenv('CI') ? "${version}.${System.getenv('CIRCLE_BUILD_NUM')}" : version
    }
}

springBoot {
    buildInfo {
        properties {
            artifact = rootProject.name
            version = System.getenv('CI') ? "${version}.${System.getenv('CIRCLE_BUILD_NUM')}" : version
            group = 'uk.gov.justice.digital'
            name = rootProject.name
            time = today
            additional = [
                    by                   : System.properties['user.name'],
                    operatingSystem      : "${System.properties['os.name']} (${System.properties['os.version']})",
                    continuousIntegration: System.getenv('CI') ? true : false,
                    machine              : InetAddress.localHost.hostName
            ]
        }
    }
}

// Apply application plugin for easy application start
apply plugin: 'application'

mainClassName = "uk.gov.justice.digital.nomis.CustodyApiApplication"

// Apply the groovy plugin to also add support for Groovy (needed for Spock)
apply plugin: 'groovy'

// In this section you declare where to find the dependencies of your project
repositories {
    // Use 'jcenter' for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
    mavenLocal()
}


// In this section you declare the dependencies for your production and test code
dependencies {
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    compile files('lib/ojdbc8-12.2.0.1.jar')

    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.9.8'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.8'
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.springframework.boot:spring-boot-starter-security'
    compile("org.springframework.security.oauth:spring-security-oauth2:2.3.4.RELEASE")
    compile("org.springframework.security:spring-security-jwt:1.0.9.RELEASE")
    
    compile 'javax.annotation:javax.annotation-api:1.3.2'
    compile 'javax.xml.bind:jaxb-api:2.3.0'
    compile 'com.sun.xml.bind:jaxb-impl:2.3.0'
    compile 'com.sun.xml.bind:jaxb-core:2.3.0'
    compile 'javax.activation:activation:1.1.1'

    compile 'org.hibernate:hibernate-java8:5.4.0.Final'
    compile 'org.springframework.hateoas:spring-hateoas:0.25.0.RELEASE'
    compile 'io.springfox:springfox-swagger2:2.9.2'
    compile 'io.springfox:springfox-swagger-ui:2.9.2'
    compile 'com.zaxxer:HikariCP:3.2.0'
    compile 'commons-io:commons-io:2.6'
    compile 'org.apache.commons:commons-lang3:3.8.1'
    compile 'net.logstash.logback:logstash-logback-encoder:5.2'
    compile 'com.microsoft.azure:applicationinsights-core:2.3.0'
    compile 'com.microsoft.azure:applicationinsights-web:2.3.0'
    compile 'com.microsoft.azure:applicationinsights-logging-logback:2.3.0'

    annotationProcessor 'org.projectlombok:lombok:1.18.4'
    compileOnly 'org.projectlombok:lombok:1.18.4'
    
    testRuntime 'com.h2database:h2:1.4.197'

    runtime 'com.h2database:h2:1.4.197'

    testCompile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'junit:junit:4.12'
    testCompile 'io.rest-assured:rest-assured:3.2.0'
    testCompile "com.github.tomakehurst:wiremock:2.20.0"

}
